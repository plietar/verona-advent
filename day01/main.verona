use "../lib/list.verona"
use "../lib/tree.verona"
use "../lib/parser.verona"

class Main
{
  // Type inference was picking List[U64] as the return type of List.create(),
  // instead of List[U64 & imm], even though the list got passed to print_list.
  //
  // We use a separate function to ascribe the type we actually want.
  make_list(): List[U64 & imm] & iso {
    List.create()
  }

  part1(node: (ListNode[U64 & imm] & mut) | (None & imm), target: U64 & imm): (U64 & imm) | (None & imm)
  {
    Main._part1(node, mut-view (Tree.create()), target)
  }

  // Pattern matching on a loop variable seems to cause lots of problems during
  // type inference. Use recursion to work around this.
  _part1(node: (ListNode[U64 & imm] & mut) | (None & imm), seen: Tree[U64 & imm] & mut, target: U64 & imm): (U64 & imm) | (None & imm)
  {
    match node {
      var node: ListNode[U64 & imm] => {
        var partner = target - node.value;
        if seen.contains(partner) {
          partner * node.value
        } else {
          seen.insert(node.value);
          Main._part1(node.next, seen, target)
        }
      }
      var _: None => None.create(),
    }
  }

  part2(node: (ListNode[U64 & imm] & mut) | (None & imm)): (U64 & imm) | (None & imm)
  {
    match node {
      var node: ListNode[U64 & imm] => {
        var target = 2020 - node.value;
        var seen = mut-view (Tree.create());
        match Main.part1(node.next, target) {
          var product: U64 => {
            product * node.value
          }
          var _: None => {
            Main.part2(node.next)
          }
        }
      }
      var _: None => None.create(),
    }
  }

  // 224436
  // 303394260

  load(): List[U64 & imm] & iso {
    var data = Main.make_list();
    var parser = mut-view (Parser.create());

    while ({ parser.skip_ws(); parser.peek() != IO.eof() }) {
      var value = parser.int();
      (mut-view data).push_back(value);
    };

    data
  }

  main() {
    var data = mut-view (Main.load());

    Builtin.print1("Part 1: {}\n", Main.part1(data.head, 2020));
    Builtin.print1("Part 2: {}\n", Main.part2(data.head));
  }
}
