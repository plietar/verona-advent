use "../lib/parser.verona"

class Main
{
  read_int(parser: Parser & mut): U64 & imm {
    match parser.int() {
      var v: U64 => v,
      var _: EOF => {
        Builtin.print("Unexpected EOF");
        Builtin.abort()
      }
      var _: ParseError => {
        Builtin.print("Parser error");
        Builtin.abort()
      }
    }
  }

  main() {
    var valid1 = 0; // Part 1 result
    var valid2 = 0; // Part 2 result

    var parser = mut-view (Parser.create());
    while ({ parser.skip_ws(); parser.peek() != IO.eof() }) {
      var low = Main.read_int(parser);
      parser.consume(); // '-'
      var high = Main.read_int(parser);
      parser.skip_ws();
      var c = parser.consume();

      parser.consume(); // ':'
      parser.skip_ws();

      var occurences1 = 0; // Occurences of the character at any position.
      var occurences2 = 0; // Occurences of the character at positions `low` or `high`.

      var pos = 0;
      while parser.peek() != 10 {
        pos = pos + 1;
        if parser.peek() == c {
          occurences1 = occurences1 + 1;
          if (pos == low) || (pos == high) {
            occurences2 = occurences2 + 1;
          }
        } else {};
        parser.consume();
      };

      if (occurences1 >= low) && (occurences1 <= high) {
        valid1 = valid1 + 1;
      } else { };

      if occurences2 == 1 {
        valid2 = valid2 + 1;
      } else { };
    };

    Builtin.print1("Part 1: {}\n", valid1);
    Builtin.print1("Part 2: {}\n", valid2);
  }
}
